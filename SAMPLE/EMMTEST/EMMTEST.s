MONITOR_80K	EQU		0082H
MONITOR_700	EQU		00ADH
PRTBYT		EQU		03C3H
GETL		EQU		0003H
GETKEY		EQU		001BH
MSGPR		EQU		0015H
PRNT		EQU		0012H
LETLN		EQU		0006H
DISPCH		EQU		0DB5H
DPCT		EQU		0DDCH
LBUF		EQU		11A3H
IBUFE		EQU		10F0H
FNAME		EQU		10F1H
FSIZE		EQU		1102H
SADRS		EQU		1104H

EMM		EQU		00H



		ORG	1200H

		LD	DE,TITLE			;'******** EMM RAM TEST MENU ********'表示
		CALL	MSGPR
		CALL	LETLN
MENU:		CALL	LETLN
		LD	DE,EMENU			;'0)EMM0 1)EMM1 2)EMM2 3)EMM3 4)EXIT?'表示
		CALL	MSGPR
EKEY:		CALL	GETKEY				;1文字入力待ち
		CP	00H
		JR	Z,EKEY
		LD	B,A
DLY:		CALL	GETKEY				;チャタリングキャンセル
		CP	B
		JR	Z,DLY
		LD	A,B
EMM0:		CP	'0'				;EMM0
		JR	NZ,EMM1
		CALL	PRNT
		LD	A,00H				;EMM I/Oアドレス 00H
		CALL	EMMADRS
		JR	MENUS
EMM1:		CP	'1'				;EMM1
		JR	NZ,EMM2
		CALL	PRNT
		LD	A,04H				;EMM I/Oアドレス 04H
		CALL	EMMADRS
		JR	MENUS
EMM2:		CP	'2'				;EMM2
		JR	NZ,EMM3
		CALL	PRNT
		LD	A,08H				;EMM I/Oアドレス 08H
		CALL	EMMADRS
		JR	MENUS
EMM3:		CP	'3'				;EMM3
		JR	NZ,EMM4
		CALL	PRNT
		LD	A,0CH				;EMM I/Oアドレス 0CH
		CALL	EMMADRS
		JR	MENUS
EMM4:		CP	'4'				;EXIT
		JR	NZ,MENU
		CALL	PRNT
		CALL	LETLN
		JR	EXIT
		
	
MENUS:		LD	A,B
		LD	(MSG1+3),A			;EMM0～EMM3の文字を変更
		LD	(MSG4+3),A
MS2:		CALL	LETLN
		JR	LSTART				;EMMLOAD

EXIT:		CALL	LETLN
		LD	DE,ENDMSG			;'RUN AGAIN:[J12A0]'表示
		CALL	MSGPR
		JP	MONITOR

LSTART:
;********************************** EMMLOAD処理本体 ************************************
ST0:		CALL	LETLN
ML1:
		LD	DE,MSG0				;' CHECK START!'表示
		CALL	MSGPR

		XOR	A
		LD	(ADRSL),A
		LD	(ADRSM),A
		LD	(ADRSH),A

		CALL	LETLN
LOP1:
		LD	A,(ADRSL)
		AND	A
		JR	NZ,LOP2
		CALL	PRTADRS
		CALL	LETLN
LOP2:
		CALL	EMMREAD
		LD	(WORK1),A
		LD	A,55H
		CALL	EMMTEST
		CP	55H
		JR	NZ,EMMERR
		LD	A,0AAH
		CALL	EMMTEST
		CP	0AAH
		JR	NZ,EMMERR
		LD	A,(ADRSL)
		CALL	EMMTEST
		LD	HL,ADRSL
		CP	(HL)
		JR	NZ,EMMERR
		LD	A,(WORK1)
		CALL	EMMTEST
		LD	HL,WORK1
		CP	(HL)
		JR	NZ,EMMERR
		LD	HL,(ADRSL)
		INC	HL
		LD	(ADRSL),HL
		LD	A,L
		OR	H
		JR	NZ,LOP1
		LD	A,(ADRSH)
		INC	A
		LD	(ADRSH),A
		CP	08H
		JR	NZ,LOP1
		
		LD	DE,MSG1				;'EMMx MEMORY TEST END'を表示
		CALL	MSGPR
		JP	EXIT				;終了

EMMERR:
		CALL	LETLN
		LD	DE,MSG4				;'EMMx CHECK ERROR!'を表示
		CALL	MSGPR
		JP	EXIT				;終了

EMMTEST:
		CALL	EMMWRITE
		CALL	EMMREAD
		RET

EMMREAD:
		LD	A,(ADRSL)			;EMMアドレスセット
ML2:		OUT	(EMM),A
		LD	A,(ADRSM)
		OUT	(EMM+1),A
		LD	A,(ADRSH)
		OUT	(EMM+2),A
		IN	A,(EMM+3)
		RET

EMMWRITE:
		PUSH	AF
		LD	A,(ADRSL)			;EMMアドレスセット
ML4:		OUT	(EMM),A
		LD	A,(ADRSM)
		OUT	(EMM+1),A
		LD	A,(ADRSH)
		OUT	(EMM+2),A
		POP	AF
		OUT	(EMM+3),A
		RET

EMMADRS:
		LD	(ML2+1),A
		LD	(ML4+1),A
		INC	A
		LD	(ML2+6),A
		LD	(ML4+6),A
		INC	A
		LD	(ML2+11),A
		LD	(ML4+11),A
		INC	A
		LD	(ML2+13),A
		LD	(ML4+14),A
		RET

PRTADRS:
		LD	A,(ADRSH)
		CALL	PRTBYT
		LD	A,(ADRSM)
		CALL	PRTBYT
		LD	A,(ADRSL)
		CALL	PRTBYT
		RET
		
MONITOR:	LD	HL,014EH
		LD	A,(HL)
		CP	'P'				;014EHが'P'ならMZ-80K
		JP	Z,MONITOR_80K
		CP	'N'				;014EHが'N'ならFN-700
		JP	Z,MONITOR_80K
		LD	HL,06EBH
		LD	A,(HL)
		CP	'M'				;06EBHが'M'ならMZ-700
		JP	Z,MONITOR_700
		JP	0000H				;識別できなかったら0000Hへジャンプ

TITLE:
		DB	'******** EMM RAM TEST MENU ********'
		DB	0DH

EMENU:		DB	'0)EMM0 1)EMM1 2)EMM2 3)EMM3 4)EXIT?'
		DB	0DH

ENDMSG:		DB	'RUN AGAIN:[J1200]'
		DB	0DH

MSG0:		DB	' EMM RAM TEST START!'
		DB	0DH

MSG1:
		DB	'EMM0 RAM OK!'
		DB	0DH

MSG4:
		DB	'EMM0 RAM ERROR!'
		DB	0DH

ADRSL:		DB	00H
ADRSM:		DB	00H
ADRSH:		DB	00H
WORK1:		DB	00H

		END

